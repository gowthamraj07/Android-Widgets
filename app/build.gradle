apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'


def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().split('-')[0].trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.androidwidgets"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'com.google.android.material:material:1.6.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.4.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(path: ':libraries:formatedittext')
    implementation project(path: ':libraries:listview')

    implementation 'com.google.firebase:firebase-analytics:20.0.0'

    // Navigation component
    def nav_version = "2.4.0-beta02"
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"

    implementation 'org.codehaus.groovy:groovy-all:2.4.12'
}
