apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().split('-')[0].trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.androidwidgets"
        minSdkVersion 21
        targetSdk 34
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD

            // Optional, specify signing versions used
            // v1SigningEnabled true
            // v2SigningEnabled true
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.androidwidgets.widgets'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation AndroidX.appCompat
    implementation AndroidX.constraintLayout
    implementation Google.android.material
    implementation AndroidX.appCompat

    implementation 'androidx.legacy:legacy-support-v4:_'
    androidTestImplementation AndroidX.test.ext.junit
    androidTestImplementation AndroidX.test.espresso.core

    implementation project(path: ':libraries:formatedittext')
    implementation project(path: ':libraries:listview')

    implementation 'com.google.firebase:firebase-analytics:17.5.0'

    // Navigation component
    def nav_version = "2.3.0-alpha03"
    // Java language implementation
    implementation AndroidX.navigation.fragment
    implementation AndroidX.navigation.ui
    // Testing Navigation
    androidTestImplementation AndroidX.navigation.testing
}
